LICENSE = "Apache-2.0"
LIC_FILES_CHKSUM = "file://LICENSE;md5=86d3f3a95c324c9479bd8986968f4327"

SRC_URI = "gitsm://git@github.com/InSol-Tech/firmware-cm-everest.git;branch=feat/PMU-392_yocto_synch;protocol=ssh"
SRCREV = "4e0741f9ddab3f227be48e5efede127ff8dc5afe"

inherit cmake

S = "${WORKDIR}/git"

DEPENDS += " \
    pkgconfig-native \
    evcli-native \
    everest-cmake \
    everest-core \
    everest-framework \
    libocpp \
    libnfc \
"

EXTRA_OECMAKE += " \
    -DDISABLE_EDM=ON \
    -DNO_FETCH_CONTENT=ON \
    -DEVEREST_ENABLE_RUN_SCRIPT_GENERATION=OFF \
    -DEVEREST_PROJECT_DIRS:STRING=${STAGING_DIR_TARGET}/usr/share/everest/ \
    -Deverest-evert_USE_PYTHON_VENV=OFF \
    -DCMAKE_SKIP_RPATH=TRUE \
"

do_install:append() {
    # Remove version file generated by EVerest core
    rm -f ${D}${datadir}/everest/version_information.txt

    # Copy runconfig YAML files
    install -d ${D}${sysconfdir}/evert-firmware-cm-everest/config/runconfig
    cp -r ${S}/config/runconfig/yocto/*.yaml ${D}${sysconfdir}/evert-firmware-cm-everest/config/runconfig/

    # Copy logging configuration
    install -d ${D}${sysconfdir}/evert-firmware-cm-everest/config/logs
    cp -r ${S}/config/logs/* ${D}${sysconfdir}/evert-firmware-cm-everest/config/logs/

    # Copy OCPP configuration files
    install -d ${D}${sysconfdir}/evert-firmware-cm-everest/config/ocpp/v16
    install -m 0644 ${S}/config/ocpp/v16/config.json ${D}${sysconfdir}/evert-firmware-cm-everest/config/ocpp/v16/
    # 'user_config.json' is not commmitted, it is created at runtime
    # install -m 0644 ${S}/config/ocpp/v16/user_config.json ${D}${sysconfdir}/evert-firmware-cm-everest/config/ocpp/v16/

    # Create OCPP logs and database directories
    install -d ${D}${sysconfdir}/evert-firmware-cm-everest/Helper/ocpp16/logs
    install -d ${D}${sysconfdir}/evert-firmware-cm-everest/Helper/ocpp16/db

    # Create EVSE logs directory
    install -d ${D}${sysconfdir}/evert-firmware-cm-everest/Helper/evse/everest-logs
}

FILES:${PN} += " \
    ${datadir}/everest/* \
    ${sysconfdir}/evert-firmware-cm-everest \
"
